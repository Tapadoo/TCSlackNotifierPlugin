buildscript {
  ext.kotlin_version = '1.3.50'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

repositories {
  mavenCentral()

  maven { url "https://download.jetbrains.com/teamcity-repository/" }
}

apply plugin: 'kotlin'

sourceCompatibility = 1.8
version = VERSION
println("##teamcity[buildNumber '$version']")

configurations {
  deploy
}

dependencies {
  def teamcity = '2019.1.3'
  def gson = 'com.google.code.gson:gson:2.8.5'
  def joda_time = 'joda-time:joda-time:2.10.4'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  implementation "org.jetbrains.teamcity:server-api:$teamcity"
  implementation "org.jetbrains.teamcity:common-api:$teamcity"
  implementation 'org.jdom:jdom:2.0.2'
  implementation gson
  implementation joda_time

  deploy gson
  deploy joda_time

  testImplementation 'junit:junit:4.12'
}

/**
 * not using apply plugin: 'distribution' and distributions closure as it makes a sub-folder in zip which I do not want
 */
task buildZip(dependsOn: 'jar', type: Zip) {
  from('.')
  include('teamcity-plugin.xml')

  into('server') {
    from jar.outputs.files
    include('*.jar')

    from configurations.deploy
    include('*.jar')
  }
}

wrapper {
  gradleVersion = "5.6.2"
  distributionType = Wrapper.DistributionType.ALL
}